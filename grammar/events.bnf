<part>                       = events?
<events>                     = event (<ws> event)* <ows>
<events-inline>              = event (<ws-inline> event)* <ows-inline>
<events-inside-voice>        = event-inside-voice (<ws> event-inside-voice)*
<events-inside-voice-inline> = event-inside-voice (<ws-inline> event-inside-voice)*

    (* notes, chords & other events *)

<event>              = single-event | repeat | voices
<event-inside-voice> = single-event | repeat

<single-event>       = (octave-up | octave-down)*
                       (chord | note | rest)
                       (octave-up | octave-down)* |

                       octave-change | clj-expr-cached | marker | at-marker |
                       barline | event-sequence | cram | set-variable |
                       get-variable

repeat               = single-event <ows> <"*"> <ows> positive-number

    (* chords, notes, rests *)

chord                = (note | rest) subchord+
<subchord>           = <ows> inside-chord*
                       <ows> <"/">
                       <ows> inside-chord*
                       <ows> (note | rest)
<inside-chord>       = octave-change | clj-expr-cached

note                 = pitch duration? (<ows> slur)?
rest                 = <"r"> duration?

    (* pitch *)

pitch                = #"[a-g]" accidental*
<accidental>         = flat | sharp | natural
flat                 = "-"
sharp                = "+"
natural              = "_"

    (* octaves *)

<octave-change>      = octave-set | (octave-up | octave-down)+
octave-set           = <"o"> number
octave-up            = <">">
octave-down          = <"<">

    (* markers *)

marker               = <"%"> name
at-marker            = <"@"> name
